!======================================================================!
program sucrose
!----------------------------------------------------------------------!
implicit none
!----------------------------------------------------------------------!
character (len =   4) :: fid ! For output filename
character (len = 200) :: filename ! Ouput filename
!----------------------------------------------------------------------!
real :: KSu   ! Neutral sucrose fraction                 (fraction of M)
real :: n     ! Hill coefficient                              (unitless)
real :: alpha ! Potential rate of photosynthesis         (kg{Su}/m2/day) 
real :: beta  ! Potential rate of growth                 (kg{Su}/m2/day)
real :: dt    ! Timestep                                           (day)
real :: M     ! Structural mass                              (kg(DM)/m2)
real :: fSu   ! Sucrose fraction                         (fraction of M)
real :: Su    ! Sucrose mass                                 (kg(Su)/m2)
real :: fa, fi, qin, qout
!----------------------------------------------------------------------!
integer :: i, j, k ! Loop counts
!----------------------------------------------------------------------!
write (*,*) 'Sucrose is running...'
!----------------------------------------------------------------------!
! Run to test convergence (Fig. 2).
! Loop over different initial sucrose fractions.
!----------------------------------------------------------------------!
! Neutral sucrose fraction                               (fraction of M)
!----------------------------------------------------------------------!
KSu   = 0.028
!----------------------------------------------------------------------!
! Hill coefficient                                            (unitless)
!----------------------------------------------------------------------!
n     = 4.0
!----------------------------------------------------------------------!
! Potential rate of photosynthesis                       (kg{Su}/m2/day)
!----------------------------------------------------------------------!
alpha = 13.14e-3
!----------------------------------------------------------------------!
! Potential rate of growth                               (kg{Su}/m2/day)
!----------------------------------------------------------------------!
beta  = 13.14e-3
!----------------------------------------------------------------------!
! Timestep                                                         (day)
!----------------------------------------------------------------------!
dt    = 1.0
!----------------------------------------------------------------------!
! Structural mass                                            (kg(DM)/m2)
!----------------------------------------------------------------------!
M     = 48.1
!----------------------------------------------------------------------!
! Initial sucrose mass                                       (kg(Su)/m2)
!----------------------------------------------------------------------!
Su    = 0.0
!----------------------------------------------------------------------!
! Sucrose fraction                                       (fraction of M)
!----------------------------------------------------------------------!
fSu   = Su / M
!----------------------------------------------------------------------!
![2][2][2][2][2][2][2][2][2][2][2][2][2][2][2][2][2][2][2][2][2][2][2][!
!----------------------------------------------------------------------!
! Loop over initial Su values for Figure 1.
!----------------------------------------------------------------------!
do j = 1, 11
  !--------------------------------------------------------------------!
  ! Set string to convergence number.
  !--------------------------------------------------------------------!
  write (fid, '(i4)') j
  if (j > 999) then
    fid = fid
  else if (j > 99) then
    fid (1:1) = '0'
  else if (j > 9) then
    fid (1:2) = '00'
  else
    fid (1:3) = '000'
  end if
  !--------------------------------------------------------------------!
  filename = 'OUTPUT/diag_'//fid//'.txt'
  write (*,*) 'Opening ',trim(filename)
  open (20, file = filename, status = "unknown")
  write (20,*) '0', fSu
  !--------------------------------------------------------------------!
  ! Loop over timesteps (days)
  !--------------------------------------------------------------------!
  do i = 1, 400
    !------------------------------------------------------------------!
    ! Advance state variables one timestep.
    !------------------------------------------------------------------!
    call advance (KSu, n, alpha, beta, dt, M, fSu, Su, fa, fi, qin, &
                  qout)
    !------------------------------------------------------------------!
    ! Write output to 'OUTPUT/diag_'//fid//'.txt' for Figure 2.
    !------------------------------------------------------------------!
    write (20,*) i, fSu
    !------------------------------------------------------------------!
  end do
  !--------------------------------------------------------------------!
  close (20)
  !--------------------------------------------------------------------!
  Su = M * float (j) * 0.00576
  fSu = Su / M
  !--------------------------------------------------------------------!
end do ! j = 1, 11
!----------------------------------------------------------------------!
![3][3][3][3][3][3][3][3][3][3][3][3][3][3][3][3][3][3][3][3][3][3][3][!
!----------------------------------------------------------------------!
! Run to test response to changing source rates at different Hill
! coefficients.
!----------------------------------------------------------------------!
! Open output file.
!----------------------------------------------------------------------!
open (20,file='a.txt',status='unknown')
!----------------------------------------------------------------------!
! Initial Hill coefficient (unitless)
!----------------------------------------------------------------------!
n = 2.0
!----------------------------------------------------------------------!
! Loop over different Hill coefficients.
!----------------------------------------------------------------------!
do k = 1, 3
  !--------------------------------------------------------------------!
  ! Initial potential source rate (kg{Su}/m2/day)
  !--------------------------------------------------------------------!
  alpha = 1.0e-6
  !--------------------------------------------------------------------!
  ! Loop over potential sink rates.
  !--------------------------------------------------------------------!
  do j = 1, 201
    !------------------------------------------------------------------!
    ! Initial sucrose mass (kg(Su)/m2)
    !------------------------------------------------------------------!
    Su = 0.0
    !------------------------------------------------------------------!
    ! Sucrose fraction (fraction of M)
    !------------------------------------------------------------------!
    fSu = Su / M
    !------------------------------------------------------------------!
    ! Loop over days to integrate to equilibrium.
    !------------------------------------------------------------------!
    do i = 1, 500
      !----------------------------------------------------------------!
      ! Advance state variables one timestep.
      !----------------------------------------------------------------!
      call advance (KSu, n, alpha, beta, dt, M, fSu, Su, fa, fi, qin, &
                    qout)
      !----------------------------------------------------------------!
    end do ! i = 1, 500
    !------------------------------------------------------------------!
    ! Write out final condition at this alpha.
    !------------------------------------------------------------------!
    write (20,'(i5,8f15.6)') j,1000.0*alpha,1000.0*qout, &
      1000.0*(alpha/2.0), fSu, fi, fa, beta*beta/((alpha+beta)**2),0.5
    !------------------------------------------------------------------!
    ! Increment potential sink rate.
    !------------------------------------------------------------------!
    alpha = alpha + (2.0 * 13.14e-3 - 0.001e-3) / 200.0
   !------------------------------------------------------------------!
  end do ! j = 1, 201
  n = n + 2.0
end do ! k = 1, 3
!----------------------------------------------------------------------!
close (20)
!----------------------------------------------------------------------!
![3][3][3][3][3][3][3][3][3][3][3][3][3][3][3][3][3][3][3][3][3][3][3][!
!----------------------------------------------------------------------!
! Run to test response to changing sink rates at different Hill
! coefficients.
!----------------------------------------------------------------------!
! Open output file.
!----------------------------------------------------------------------!
open (20,file='b.txt',status='unknown')
!----------------------------------------------------------------------!
! Reset initial potential source rate (kg{Su}/m2/day)
!----------------------------------------------------------------------!
alpha = 13.14e-3
!----------------------------------------------------------------------!
! Initial Hill coefficient (unitless)
!----------------------------------------------------------------------!
n = 2.0
!----------------------------------------------------------------------!
! Loop over different Hill coefficients.
!----------------------------------------------------------------------!
do k = 1, 3
  !--------------------------------------------------------------------!
  ! Initial potential sink rate (kg{Su}/m2/day)
  !--------------------------------------------------------------------!
  beta = 1.0e-6
  !--------------------------------------------------------------------!
  ! Loop over potential sink rates.
  !--------------------------------------------------------------------!
  do j = 1, 201
    !------------------------------------------------------------------!
    ! Initial sucrose mass (kg(Su)/m2)
    !------------------------------------------------------------------!
    Su = 0.0
    !------------------------------------------------------------------!
    ! Sucrose fraction (fraction of M)
    !------------------------------------------------------------------!
    fSu = Su / M
    !------------------------------------------------------------------!
    ! Loop over days to integrate to equilibrium.
    !------------------------------------------------------------------!
    do i = 1, 500
      !----------------------------------------------------------------!
      ! Advance state variables one timestep.
      !----------------------------------------------------------------!
      call advance (KSu, n, alpha, beta, dt, M, fSu, Su, fa, fi, qin, &
                    qout)
      !----------------------------------------------------------------!
    end do ! i = 1, 500
    !------------------------------------------------------------------!
    ! Write out final condition at this alpha.
    !------------------------------------------------------------------!
    write (20,'(i5,8f15.6)') j,1000.0*beta,1000.0*qout, &
      1000.0*(alpha/2.0), fSu, fi, fa, alpha*alpha/((alpha+beta)**2),0.5
    !------------------------------------------------------------------!
    ! Increment potential sink rate.
    !------------------------------------------------------------------!
    beta = beta + (2.0 * 13.14e-3 - 0.001e-3) / 200.0
   !------------------------------------------------------------------!
  end do ! j = 1, 201
  n = n + 2.0
end do ! k = 1, 3
!----------------------------------------------------------------------!
close (20)
!----------------------------------------------------------------------!
![4][4][4][4][4][4][4][4][4][4][4][4][4][4][4][4][4][4][4][4][4][4][4][!
!----------------------------------------------------------------------!
! Starvation response.
!----------------------------------------------------------------------!
Su    = 0.0
alpha = 13.14e-3
beta  = 13.14e-3
n     = 4.0
fSu = Su / M
open (20,file='starve.txt',status='unknown')
do i = 1, 2000
  if (i > 500) then
    alpha = 0.0
  end if
  call advance (KSu, n, alpha, beta, dt, M, fSu, Su, fa, fi, qin, &
                    qout)
  write (*,*) i, fSu, qout
  write (20,*) i,fSu, qout
end do
close (20)
!----------------------------------------------------------------------!
! No growth response.
!----------------------------------------------------------------------!
Su    = 0.0
alpha = 13.14e-3
beta  = 13.14e-3
fSu = Su / M
open (20,file='drought.txt',status='unknown')
do i = 1, 2000
  if (i > 500) then
    beta = 0.0
  end if
  call advance (KSu, n, alpha, beta, dt, M, fSu, Su, fa, fi, qin, &
                    qout)
  write (*,*) i, fSu, qin
  write (20,*) i,fSu, qin
end do
close (20)
!----------------------------------------------------------------------!
![][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][][]!
!----------------------------------------------------------------------!
write (*,*) 'Sucrose has finished...'
!----------------------------------------------------------------------!
end program sucrose
!======================================================================!
subroutine advance (KSu, n, alpha, beta, dt, M, fSu, Su, fa, fi, qin, &
                    qout)
!----------------------------------------------------------------------!
! Advance state variables one timestep.
!----------------------------------------------------------------------!
implicit none
!----------------------------------------------------------------------!
real, intent(in) :: KSu
real, intent(in) :: n
real, intent(in) :: alpha
real, intent(in) :: beta
real, intent(in) :: dt
real, intent(in) :: M
real, intent(inout) :: fSu
real, intent(inout) :: Su
real, intent(out) :: fa
real, intent(out) :: fi
real, intent(out) :: qin
real, intent(out) :: qout
real :: dSu
!----------------------------------------------------------------------!
! Activation factor; Eqn. (2)                         (Eqn. (2); scalar)
!----------------------------------------------------------------------!
fa = (fSu ** n) / (KSu ** n + fSu ** n)
!----------------------------------------------------------------------!
! Inhibition factor; Eqn. (3)                                   (scaler)
!----------------------------------------------------------------------!
fi = (KSu ** n) / (KSu ** n + fSu ** n)
!----------------------------------------------------------------------!
! C source flux; Eqn. (4)                                (kg(Su)/m2/day)
!----------------------------------------------------------------------!
qin  = fi * alpha
!----------------------------------------------------------------------!
! C sink flux Eqn. (4)                                   (kg(Su)/m2/day)
!----------------------------------------------------------------------!
qout = fa * beta
!----------------------------------------------------------------------!
! Change in sucrose C mass; Eqn. (1)                     (kg(Su)/m2/day)
!----------------------------------------------------------------------!
dSu = qin - qout
!----------------------------------------------------------------------!
! Integrate Su                                               (kg(Su)/m2)
!----------------------------------------------------------------------!
Su = Su + dt * dSu
!----------------------------------------------------------------------!
! New sucrose fraction                                        (fraction)
!----------------------------------------------------------------------!
fSu = Su / M
!----------------------------------------------------------------------!
end subroutine advance
!======================================================================!
